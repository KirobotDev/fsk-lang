class Node {
  fn init(value) {
    this.value = value;
    this.next = nil;
  }
}

class List {
  fn init() {
    this.head = nil;
    this.tail = nil;
    this.size = 0;
  }

  fn push(value) {
    let node = Node(value);
    if (this.head == nil) {
      this.head = node;
      this.tail = node;
    } else {
      this.tail.next = node;
      this.tail = node;
    }
    this.size = this.size + 1;
  }

  fn forEach(callback) {
    let current = this.head;
    while (current != nil) {
      callback(current.value);
      current = current.next;
    }
  }

  fn map(callback) {
    let newList = List();
    let current = this.head;
    while (current != nil) {
      newList.push(callback(current.value));
      current = current.next;
    }
    return newList;
  }

  fn get(index) {
    if (index >= this.size) return nil;
    let current = this.head;
    let i = 0;
    while (i < index) {
      current = current.next;
      i = i + 1;
    }
    return current.value;
  }
}
