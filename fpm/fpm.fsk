
fn printHelp() {
    print "Usage: fpm <command> [args]";
    print "Commands:";
    print "  init            Initialize a new Fsk package";
    print "  install <name>  Install a package from Registry";
    print "  install <url>   Install a package from Git URL";
    print "  publish         Publish current package to Registry";
    print "  doctor          Check system for native dependencies";
    print "  bundle <file>   Merge all imports into a single standalone file";
}

let REGISTRY = "https://fpm.kirosb.fr/api";
let argCount = FSK.argCount();

if (argCount < 2) {
    printHelp();
} else {
    let command = FSK.arg(2); 

    if (command == "init") {
        if (FSK.exists("package.fsk")) {
             print "Error: package.fsk already exists.";
        } else {
             let pkg = JSON.parse('{}');
             pkg.name = "my-project";
             pkg.version = "1.0.0";
             pkg.dependencies = JSON.parse('{}');
             
             let content = JSON.stringify(pkg);
             writeFile("package.fsk", content);
             print "Created package.fsk";
        }
    } 
    else if (command == "install") {
        if (argCount < 3) {
            print "Usage: fpm install <package_names...>";
        } 
        else {
            for (let i = 3; i < argCount; i = i + 1) {
                let target = FSK.arg(i);
                print "--- Installing " + target + " ---";
                
                let gitUrl = target;
                let isRegistry = true;

                if (FSK.indexOf(target, "http") == 0) { isRegistry = false; }
                if (FSK.indexOf(target, "git@") == 0) { isRegistry = false; }

                if (isRegistry) {
                    print "Looking for '" + target + "' in registry...";
                    let jsonStr = FSK.shell("curl -s " + REGISTRY + "/packages/" + target);
                    let pkgInfo = JSON.parse(jsonStr);

                    if (pkgInfo == nil) {
                        print "Error: Package not found or invalid JSON.";
                        gitUrl = ""; 
                    } else if (pkgInfo.url == nil) {
                        print "Error: Package info missing URL.";
                        gitUrl = "";
                    } else {
                        gitUrl = pkgInfo.url;
                        print "Found source: " + gitUrl;
                    }
                }

                if (gitUrl != "") {
                    if (FSK.exists("fsk_modules") == false) {
                        FSK.mkdir("fsk_modules");
                    }
                    
                    print "Cloning into fsk_modules...";
                    
                    let cmd = "cd fsk_modules && git clone " + gitUrl;
                    let res = FSK.shell(cmd);
                    print res;
                    print "Installed " + target;
                    
                    let lockData = JSON.parse('{}');
                    if (FSK.exists("fpm.lock")) {
                         lockData = JSON.parse(readFile("fpm.lock"));
                    }
                    
                    let pkgEntry = JSON.parse('{}');
                    pkgEntry.url = gitUrl;
                    pkgEntry.installed_at = Date.now();
                    pkgEntry.source = isRegistry ? "registry" : "git";
                    
                    lockData[target] = pkgEntry;
                    writeFile("fpm.lock", JSON.stringify(lockData));
                    print "Updated fpm.lock";
                }
            }
        }
    } 
    else if (command == "publish") {
        print "--- FPM PUBLISH ---";
        let name = input("Package Name: ");
        let url = input("Git Repository URL: ");
        let desc = input("Description: ");
        let author = input("Author: ");

        let payloadObj = JSON.parse('{}');
        payloadObj.name = name;
        payloadObj.url = url;
        payloadObj.description = desc;
        payloadObj.author = author;
        
        let payload = JSON.stringify(payloadObj);
        let tmpFile = "fpm_payload.json";
        writeFile(tmpFile, payload);
        
        let curlCmd = "curl -s -X POST -H 'Content-Type: application/json' -d @" + tmpFile + " " + REGISTRY + "/publish";
        
        print "Sending to registry...";
        let response = FSK.shell(curlCmd);
        print "Response: " + response;
        
    } 
    else if (command == "doctor") {
        print "--- FPM DOCTOR ---";
        print "Checking system dependencies...";
        
        let hasRaylib = false;
        if (FSK.exists("/usr/local/lib/libraylib.so")) { hasRaylib = true; }
        if (FSK.exists("/usr/lib/libraylib.so")) { hasRaylib = true; }
        
        if (hasRaylib) {
            print "[OK] Raylib is installed.";
        } else {
            print "[!!] Raylib is MISSING.";
            let choice = input("Would you like FPM to install Raylib for you? (y/n): ");
            if (choice == "y") {
                print "Building Raylib from source... (This may take a minute)";
                let res = FSK.shell("bash scripts/install_raylib.sh");
                print res;
                print "Raylib installation finished.";
            }
        }
    }
    else if (command == "bundle") {
        if (argCount < 3) {
            print "Usage: fpm bundle <main_file.fsk>";
        } else {
            let entryFile = FSK.arg(3);
            let outputName = "bundle.fsk";
            if (argCount >= 4) { outputName = FSK.arg(4); }
            
            print "Bundling " + entryFile + " into " + outputName + "...";
            
            let content = readFile(entryFile);
            let lines = FSK.split(content, "\n");
            let finalOutput = "// FSK BUNDLE - Generated by fpm\n";
            
            for (let i = 0; i < lines.length; i = i + 1) {
                let line = lines[i];
                if (FSK.indexOf(line, "import") == 0) {
                   let parts = FSK.split(line, '"');
                   if (parts.length >= 2) {
                       let impFile = parts[1];
                       print "  Merging " + impFile;
                       finalOutput = finalOutput + "\n// --- Merged: " + impFile + " ---\n";
                       let impContent = FSK.readFile(impFile);
                       if (impContent == "") {
                           impContent = FSK.readFile("../" + impFile);
                       }
                       finalOutput = finalOutput + impContent + "\n";
                   }
                } else {
                   finalOutput = finalOutput + line + "\n";
                }
            }
            
            writeFile(outputName, finalOutput);
            print "Done! Bundle created at " + outputName;
        }
    }
    else {
        print "Unknown command: " + command;
        printHelp();
    }
}
